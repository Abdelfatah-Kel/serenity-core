ext {
    bintrayPackage = 'serenity-spring'
    projectDescription = 'Serenity Spring integration'
}

dependencies {
    compile project(':serenity-core')
    compile project(':serenity-junit')
    compile "junit:junit:${junitVersion}"
    compile ("org.springframework:spring-test:${springVersion}")
    compile ("org.springframework:spring-context:${springVersion}")
    compile ("org.springframework:spring-context-support:${springVersion}")

    // Unit testing
    testCompile "org.springframework:spring-jdbc:${springVersion}"
    testCompile "org.springframework:spring-aop:${springVersion}"
    testCompile "org.springframework:spring-orm:${springVersion}"
    testCompile("org.springframework.boot:spring-boot-test:${springBootVersion}") {
        exclude group:'org.springframework', module:'spring-context'
        exclude group:'org.springframework', module:'spring-core'
    }
    testCompile 'commons-dbcp:commons-dbcp:1.4'
    testCompile 'javax.annotation:javax.annotation-api:1.3.2'
    testCompile ("org.hibernate:hibernate-entitymanager:5.2.12.Final") {
        exclude group: 'commons-logging'
        exclude group: 'org.javassist'
        exclude group: 'org.slf4j'
        exclude group: 'net.bytebuddy'
        exclude group: 'org.jboss.logging'
        exclude group: 'com.fasterxml', module:'classmate'
    }
}

test {
	if (excludes.contains('**/integration/**')) {
		excludes -= '**/integration/**'
		exclude {
			FileTreeElement elem -> elem.path.contains('/integration/') && !elem.path.contains('/junit/spring/integration/')
		}
	}
	else {
		throw new GradleException("The root build.gradle has been modified and has broken serenity-spring's build.gradle. Please modify serenity-spring's build.gradle.")
	}
}

integrationTests {
	exclude '**/junit/spring/integration/**'
}
